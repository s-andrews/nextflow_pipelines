#!/usr/bin/env nextflow
nextflow.enable.dsl=2

// Last modified 14 October 2020

params.outdir = "."
params.genome = ""
params.verbose = false
params.single_end = false  // CLOCK Processing HAS to be paired-end!

params.fastqc_args = ''
params.fastq_screen_args = ''
params.trim_galore_args = ''
params.bismark_args = ''
params.umibam_args = ''
params.bismark_methylation_extractor_args = ''
params.bismark2summary_args = ''
params.bismark2report_args= ''
params.multiqc_args = ''

params.help = false
// Show help message and exit
if (params.help){
    helpMessage()
    exit 0
}

params.list_genomes = false;
if (params.list_genomes){
    println ("[WORKLFOW] List genomes selected")
}

if (params.verbose){
    println ("[WORKFLOW] FASTQC ARGS: "           + params.fastqc_args)
    println ("[WORKFLOW] FASTQ SCREEN ARGS ARE: " + params.fastq_screen_args)
    println ("[WORKFLOW] TRIM GALORE ARGS: "      + params.trim_galore_args)
    println ("[WORKFLOW] BISMARK ARGS ARE: "      + params.bismark_args)
    println ("[WORKFLOW] UMIBAM ARGS ARE: "       + params.umibam_args)
    println ("[WORKFLOW] BISMARK METHYLATION EXTRACTOR ARGS ARE: " + params.bismark_methylation_extractor_args)
}

include { makeFilesChannel; getFileBaseNames }     from './nf_modules/files.mod.nf'
include { getGenome }                              from './nf_modules/genomes.mod.nf'
include { listGenomes }                            from './nf_modules/genomes.mod.nf'

if (params.list_genomes){
    listGenomes()  // this lists all available genomes, and exits
}

genome = getGenome(params.genome)

include { FASTQC }                                 from './nf_modules/fastqc.mod.nf' 
include { FASTQC as FASTQC2 }                      from './nf_modules/fastqc.mod.nf'
include { FASTQC as FASTQC3 }                      from './nf_modules/fastqc.mod.nf'

// --bisulfite paramater needs to be re-enabled, but MultiQC is currently broken
include { FASTQ_SCREEN }                           from './nf_modules/fastq_screen.mod.nf' params(bisulfite: '--bisulfite')

// Adding flag for RRBS processing
// The second trimming step needs to look like this:
// trim_galore --paired --three_prime_clip_R1 15 --three_prime_clip_R2 15 *.clock_UMI.R1.fq.gz *.clock_UMI.R2.fq.gz

include { TRIM_GALORE }                            from './nf_modules/trim_galore.mod.nf' params(three_prime_clip_R1: 15, three_prime_clip_R2: 15)
include { TRIM_GALORE as TRIM_CLOCK }              from './nf_modules/trim_galore.mod.nf' params(clock: true)
include { BISMARK }                                from './nf_modules/bismark.mod.nf'     params(genome: genome)
include { UMIBAM }                                 from './nf_modules/umibam.mod.nf'      params(dual: true)

// Adding flag for RRBS processing
include { BISMARK_METHYLATION_EXTRACTOR }          from './nf_modules/bismark_methylation_extractor.mod.nf' params(rrbs: true)
include { BISMARK2REPORT }                         from './nf_modules/bismark2report.mod.nf'
include { BISMARK2SUMMARY }                        from './nf_modules/bismark2summary.mod.nf'

include { MULTIQC }                                from './nf_modules/multiqc.mod.nf' 

file_ch = makeFilesChannel(args)

workflow {

    main:
        FASTQC                         (file_ch, params.outdir, params.fastqc_args, params.verbose)
        TRIM_CLOCK                     (file_ch, params.outdir, params.trim_galore_args, params.verbose)
        FASTQC2                        (TRIM_CLOCK.out.reads, params.outdir, params.fastqc_args, params.verbose)
        TRIM_GALORE                    (TRIM_CLOCK.out.reads, params.outdir, params.trim_galore_args, params.verbose)
        FASTQC3                        (TRIM_GALORE.out.reads, params.outdir, params.fastqc_args, params.verbose)
        FASTQ_SCREEN                   (TRIM_GALORE.out.reads, params.outdir, params.fastq_screen_args, params.verbose)
        BISMARK                        (TRIM_GALORE.out.reads, params.outdir, params.bismark_args, params.verbose)
        // Need UmiBam
        UMIBAM                         (BISMARK.out.bam, params.outdir, params.umibam_args, params.verbose)

        // Skipping deduplication step
        BISMARK_METHYLATION_EXTRACTOR  (UMIBAM.out.bam, params.outdir, params.bismark_methylation_extractor_args, params.verbose)

        // merging channels for MultiQC
        multiqc_ch = FASTQC.out.report.mix(
            TRIM_GALORE.out.report,
            FASTQ_SCREEN.out.report.ifEmpty([]),
            FASTQC2.out.report.ifEmpty([]),
            BISMARK.out.report.ifEmpty([]),
            UMIBAM.out.report.ifEmpty([]),
            BISMARK_METHYLATION_EXTRACTOR.out.report.ifEmpty([]),
            BISMARK_METHYLATION_EXTRACTOR.out.mbias.ifEmpty([]),
        ).collect()

        // merging channels for Bismark reports
        bismark_report_ch = BISMARK.out.bam.mix(
            BISMARK.out.report,
            UMIBAM.out.report.ifEmpty([]),
            BISMARK_METHYLATION_EXTRACTOR.out.report.ifEmpty([]),
            BISMARK_METHYLATION_EXTRACTOR.out.mbias.ifEmpty([]),
        ).collect()

        // bismark_report_ch.subscribe {  println "Got: $it"  }

        BISMARK2REPORT                   (bismark_report_ch, params.outdir, params.bismark2report_args, params.verbose)
        BISMARK2SUMMARY                  (bismark_report_ch, params.outdir, params.bismark2summary_args, params.verbose)
        MULTIQC                          (multiqc_ch, params.outdir, params.multiqc_args, params.verbose)

}

// Since workflows with very long command lines tend to fail to get rendered at all, I was experimenting with a
// minimal execution summary report so we at least know what the working directory was...
workflow.onComplete {

    def msg = """\
        Pipeline execution summary
        ---------------------------
        Completed at: ${workflow.complete}
        Duration    : ${workflow.duration}
        Success     : ${workflow.success}
        workDir     : ${workflow.workDir}
        exit status : ${workflow.exitStatus}
        """
    .stripIndent()

    sendMail(to: "${workflow.userName}@babraham.ac.uk", subject: 'Minimal pipeline execution report', body: msg)
}

def helpMessage() {
 
    log.info"""
    >>


    SYNOPSIS:

    This workflow runs the entire RRBS based Epigenetic Clock processing pipeline (described in this publication:
    https://genomebiology.biomedcentral.com/articles/10.1186/s13059-017-1203-5) on FastQ files. This includes QC,
    Epigenetic Clock pre-processing, quality-/adapter- and RRBS-trimming, contamination QC (post-trimming), alignments to a genome using Bismark,
    UMI-aware deduplication, methylation extraction, coverage file generation, and finally generates an aggregate MultiQC report.
    Here is a graphical representation of the workflow:

    --- FastQC
    --- Trim Galore Clock
        |
        --- FastQC
        --- Trim Galore
            |
            --- FastQC
            --- FastQ Screen
            --- Bismark 
                |
                ---UmiBam
                   |
                   --- Bismark methylation extraction             
    --- bismark2report*
    --- bismark2summary*
    --- MultiQC*
        
    * These steps run only once ALL other jobs have completed.
     
    By default all these steps are submitted as jobs to the Babraham stone compute cluster.

    The involved tools are run in the following way:
    ------------------------------------------------------------
    FastQC:                           defaults
    Trim Galore Clock:                '--clock'
    Trim Galore:                      adapter auto-detection; '--three_prime_clip_R1 15 --three_prime_clip_R2 15' 
    FastQ Screen:                     '--bisulfite'
    Bismark:                          defaults
    UmiBam:                           '--dual_umi'
    Bismark methylation extraction:   '--bedGraph --buffer 10G --parallel 4'                  
    
    To add additional parameters to any of the programs, consider supplying tool-specific arguments (see --toolname_args="..." below).


         ==============================================================================================================


    USAGE:
    
    nf_bisulfite_RRBS_clock [options] --genome <genomeID> <input files>
    
    Mandatory arguments:
    ====================

      <input files>                   List of input files, e.g. '*fastq.gz' or '*fq.gz'. Files are automatically processed as
                                      single-end (SE) or paired end (PE) files (if file pairs share the same base-name, and differ only
                                      by a read number, e.g. 'base_name_R1.fastq.gz' and 'base_name_R2.fastq.gz' (or R3, R4). For
                                      PE files, only Read 1 is run through FastQ Screen (as typically R1 and R2 produce nearly identical
                                      contamination profiles). Epigenetic Clock processing has to be carried out in paired-end mode.

      --genome [str]                  Genome build ID to be used for the alignment, e.g. GRCh38 (latest human genome) or GRCm38
                                      (latest mouse genome build). To list all available genomes, see '--list_genomes' below.


    Tool-specific options:
    ======================

    For all following options, please note that the format: ="your options" needs to be strictly adhered to in order to work correctly.

      --fastqc_args="[str]"           This option can take any number of options that are compatible with FastQC to modify its default
                                      behaviour. For more detailed information on available options please refer to the FastQC documentation,
                                      or run 'fastqc --help' on the command line. As an example, to run FastQC without grouping of bases when
                                      reads are >50bp and use a specific file with non-default adapter sequences, use: 
                                      ' --fastqc_args="--nogroup --adapters ./non_default_adapter_file.txt" '. [Default: None]

      --fastq_screen_args="[str]"     This option can take any number of options that are compatible with FastQ Screen to modify its
                                      default behaviour. For more detailed information on available options please refer to the FastQ Screen
                                      documentation, or run 'fastq_screen --help' on the command line. For instance, to process a bisulfite
                                      converted library with fairly relaxed parameters, you could use: 
                                      ' --fastq_screen_args="--bisulfite --score_min L,0,-0.6" '. [Default: None]

      --trim_galore_args="[str]"      This option can take any number of options that are compatible with Trim Galore to modify its
                                      default trimming behaviour. For more detailed information on available options please refer
                                      to the Trim Galore User Guide, or run 'trim_galore --help' on the command line. As an example, to trim
                                      off the first 10bp from the 5' of R1 and 5bp of R2, use: 
                                      ' --trim_galore_args="--clip_r1 10 --clip_r2 5" '. [Default: None]

      --bismark_args="[str]"          This option can take any number of options that are compatible with Bismark to modify its
                                      default mapping behaviour. For more detailed information on available options please refer
                                      to the Bismark User Guide, or run 'bismark --help' on the comamnd line. As an example, to run somewhat
                                      relaxed alignments, use: ' --bismark_args="--score_min L,0,-0.4" '. [Default: None]

      --umibam_args="[str]"           This option can take any number of options that are compatible with UmiBam to modify its
                                      default deduplication behaviour. For more detailed information on available options please refer
                                      to the UmiBam User Guide, or run 'umibam --help' on the command line. As an example, to allow
                                      up to 1 mismatch for the UMI sequence, use: 
                                      ' --umibam_args="--mismatches 1" '. [Default: None]

      --bismark_methylation_extractor_args="[str]"   This option can take any number of options that are compatible with bismark_methylation_extractor
                                      to modify its default exctraction behaviour. For more detailed information on available options please refer
                                      to the Bismark User Guide, or run 'bismark_methylation_extractor --help' on the command line. As an example,
                                      to run the extraction and coverage file generation for a genome containing thousands of scaffolds
                                      or contigs, use: ' --bismark_methylation_extractor_args="--gazillion" '. [Default: None]


    Other options:
    ==============

      --outdir [str]                  Path to the output directory. [Default: current working directory]

      --list_genomes                  List all genome builds that are currently available to choose from. To see this list
                                      of available genomes with more detailed information about paths and indexes, run
                                      the command as '--list_genomes --verbose'
    
      --verbose                       More verbose status messages. [Default: OFF]
      --help                          Displays this help message and exits.

    Workflow options:
    =================

    Please note the single '-' hyphen for the following options!

      -resume                         If a pipeline workflow has been interrupted or stopped (e.g. by accidentally closing a laptop),
                                      this option will attempt to resume the workflow at the point it got interrupted by using
                                      Nextflow's caching mechanism. This may save a lot of time.

      -bg                             Sends the entire workflow into the background, thus disconnecting it from the terminal session.
                                      This option launches a daemon process (which will keep running on the headnode) that watches over
                                      your workflow, and submits new jobs to the SLURM queue as required. Use this option for big pipeline
                                      jobs, or whenever you do not want to watch the status progress yourself. Upon completion, the
                                      pipeline will send you an email with the job details. This option is HIGHLY RECOMMENDED!

      -process.executor=local         Temporarily changes where the workflow is executed to the 'local' machine. See also Nextflow config
                                      file for more details. [Default: slurm] 
    

    <<
    """.stripIndent()

}